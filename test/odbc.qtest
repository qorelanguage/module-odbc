#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

%new-style
%enable-all-warnings
%require-types
%strict-args

%requires QUnit

%try-module odbc
%define NoOdbc
%endtry

%exec-class OdbcTest

class OdbcTest inherits QUnit::Test {
    public {
        TimeZone utcTz = new TimeZone(0);
        *string connStr;

        # Generally compatible types will be here.
        hash Columns = {
            "sint": {
                "type": "SMALLINT",
                "values": (1, 0, 32767, -32768),
            },
            "i": {
                "type": "INTEGER",
                "values": (2147483647, 0, -1, -2147483648),
            },
            "decim": {
                "type": "DECIMAL(5,2)",
                "values": (123.45n, 0.0n, 999.99n, -999.99n),
            },
            "num": {
                "type": "NUMERIC(5,2)",
                "values": (-123.45n, 0.0n, 999.99n, -999.99n),
            },
            "fltp": {
                "type": "FLOAT(53)",
                "values": (1.72, 3.1456789765432, 0.0, -2.7218281828),
            },
            "r": {
                "type": "REAL",
                "values": (0.00390625, 0.0078125, -0.0078125, 0.0),
            },
            "flt": {
                "type": "FLOAT",
                "values": (0.00390625, 0.0078125, -0.0078125, 0.0),
            },
            "dbl": {
                "type": "DOUBLE PRECISION",
                "values": (1.72, 3.1456789765432, 0.0, -2.7218281828),
            },
            "dt": {
                "type": "DATE",
                "values": odbc_bind(ODBCT_DATE, (2016-01-11, 1970-01-01, 2020-05-20, 1987-01-30)),
                "expected": (2016-01-11, 1970-01-01, 2020-05-20, 1987-01-30),
            },
        };
        list ColumnNamesAndTypes;

        # MySQL-specific types. http://dev.mysql.com/doc/refman/5.7/en/data-types.html
        hash MysqlColumns = {
            "myvc": {
                "type": "VARCHAR(128)",
                "values": ("text", "lorem", "Příliš žluťoučký kůň úpěl ďábelské ódy", "ipsum dolor"),
            },
            "myc": {
                "type": "CHAR(128)",
                "values": ("text", "lorem", "Příliš žluťoučký kůň úpěl ďábelské ódy", "ipsum dolor"),
            },
            "mybin": {
                "type": "BINARY(64)",
                "values": (<bead>, <feed>, <beadaabbccdd>, <1122aabb3344ccdd55667788>),
                "expected": (),
            },
            "myvbin": {
                "type": "VARBINARY(64)",
                "values": (<bead>, <feed>, <beadaabbccdd>, <1122aabb3344ccdd55667788>),
            },
            "myblob": {
                "type": "BLOB(64)",
                "values": (<bead>, <feed>, <beadaabbccdd>, <1122aabb3344ccdd55667788>),
            },
            "mybool": {
                "type": "BOOLEAN",
                "values": (True, False, True, True),
                "expected": (1, 0, 1, 1),
            },
            "mytext": {
                "type": "TEXT(128)",
                "values": ("text", "lorem", "Příliš žluťoučký kůň úpěl ďábelské ódy", "ipsum dolor"),
            },
            "mybint": {
                "type": "BIGINT",
                "values": (2200100100, -9223372036854775807, 0, 9223372036854775807),
            },
            "mytint": {
                "type": "TINYINT",
                "values": (1, 0, 127, -128),
            },
            /*"tintunsig": {
                "type": "TINYINT UNSIGNED",
                "values": (1, 0, 127, 255),
            },*/
            "mymint": {
                "type": "MEDIUMINT",
                "values": (1, 0, 8388607, -8388608),
            },
            "mymintunsig": {
                "type": "MEDIUMINT UNSIGNED",
                "values": (1, 0, 127, 16777215),
            },
            /*"intunsig": {
                "type": "INTEGER UNSIGNED",
                "values": (1, 0, 127, 4294967295),
            },*/
            /*"mysqlbit": {
                "type": "BIT",
                "values": (0, 1, 1, 0),
            },*/
            "mytm": {
                "type": "TIME",
                "values": odbc_bind(ODBCT_TIME, (00:00:00Z, 05:23:17Z, 03:59:20Z, 14:15:16Z)),
                "expected": (0s, 5*60m+23m+17s, 3*60m+59m+20s, 14*60m+15m+16s),
            },
            "mydtime": {
                "type": "DATETIME",
                "values": (2016-01-11T00:00:00, 2016-01-11T05:23:17, 2016-01-11T03:00:20, 2016-01-11T14:15:16),
                "expected": (),
            },
            "myyr": {
                "type": "YEAR",
                "values": (2016-01-11, 2012-01-11, 2016-01-30, 1970-01-11),
                "expected": (2016-00-00, 2012-00-00, 2016-00-00, 1970-00-00),
            },
            "mytstamp": {
                "type": "TIMESTAMP",
                "values": (2016-01-11T00:00:00, 2016-01-11T05:23:17, 2016-01-11T03:00:20, 2016-01-11T14:15:16),
                "expected": (),
            },
        };

        # PostgreSQL-specific types. https://www.postgresql.org/docs/9.5/static/datatype.html
        hash PgsqlColumns = {
            "pgvc": {
                "type": "VARCHAR(128)",
                "values": ("text", "lorem", "Příliš žluťoučký kůň úpěl ďábelské ódy", "ipsum dolor"),
            },
            "pgc": {
                "type": "CHAR(128)",
                "values": ("text", "lorem", "Příliš žluťoučký kůň úpěl ďábelské ódy", "ipsum dolor"),
            },
            "pgbin": {
                "type": "bytea",
                "values": (<bead>, <feed>, <beadaabbccdd>, <1122aabb3344ccdd55667788>),
            },
            "pgbool": {
                "type": "BOOLEAN",
                "values": (True, False, True, True),
            },
            "pgmoney": {
                "type": "MONEY",
                "values": (123.45n, 0.0n, 92233720368547758.07n, -92233720368547758.08n),
                "expected": (123.45, 0.0, 92233720368547758.07, -92233720368547758.08),
            },
            "pgbint": {
                "type": "BIGINT",
                "values": (2200100100, -9223372036854775807, 0, 9223372036854775807),
            },
            "pgtm": {
                "type": "TIME",
                "values": odbc_bind(ODBCT_TIME, (00:00:00Z, 05:23:17Z, 03:59:20Z, 14:15:16Z)),
                "expected": (0s, 5*60m+23m+17s, 3*60m+59m+20s, 14*60m+15m+16s),
            },
            "pgtstamp": {
                "type": "TIMESTAMP",
                "values": (2016-01-11T00:00:00, 2016-01-11T05:23:17, 2016-01-11T03:00:20, 2016-01-11T14:15:16),
                "expected": (),
            },
        };

        # Oracle-specific types. https://docs.oracle.com/cd/B28359_01/server.111/b28318/datatype.htm#CNCPT012
        hash OracleColumns = {
            "orablob": {
                "type": "BLOB",
                "values": (<bead>, binary(), <beadaabbccdd>, <1122aabb3344ccdd55667788>),
                "expected": (<bead>, NULL, <beadaabbccdd>, <1122aabb3344ccdd55667788>),
            },
            "oraraw": {
                "type": "RAW(64)",
                "values": (<bead>, binary(), <beadaabbccdd>, <1122aabb3344ccdd55667788>),
                "expected": (<bead>, NULL, <beadaabbccdd>, <1122aabb3344ccdd55667788>),
            },
            "oraclob": {
                "type": "CLOB",
                "values": ("text", "lorem", "Příliš žluťoučký kůň úpěl ďábelské ódy", "ipsum dolor"),
                "expected": ("text", "lorem", "Prilis zlutoucky kun upel dabelske ody", "ipsum dolor"),
            },
            "oranclob": {
                "type": "NCLOB",
                "values": (
                    convert_encoding("text", "UTF16"),
                    convert_encoding("lorem", "UTF16"),
                    convert_encoding("Příliš žluťoučký kůň úpěl ďábelské ódy", "UTF16"),
                    convert_encoding("ipsum dolor", "UTF16")
                ),
            },
            "orac": {
                "type": "CHAR(60)",
                "values": ("text", "lorem", "Příliš žluťoučký kůň úpěl ďábelské ódy", "ipsum dolor"),
                "expected": (
                    "text                                                        ",
                    "lorem                                                       ",
                    "Prilis zlutoucky kun upel dabelske ody       ",
                    "ipsum dolor                                                 "
                ),
            },
            "oravc": {
                "type": "VARCHAR(128)",
                "values": ("text", "lorem", "Příliš žluťoučký kůň úpěl ďábelské ódy", "ipsum dolor"),
                "expected": ("text", "lorem", "Prilis zlutoucky kun upel dabelske ody", "ipsum dolor"),
            },
            "oravc2": {
                "type": "VARCHAR2(128)",
                "values": ("text", "lorem", "Příliš žluťoučký kůň úpěl ďábelské ódy", "ipsum dolor"),
                "expected": ("text", "lorem", "Prilis zlutoucky kun upel dabelske ody", "ipsum dolor"),
            },
            "oranc": {
                "type": "NCHAR(40)",
                "values": (
                    convert_encoding("text", "UTF16"),
                    convert_encoding("lorem", "UTF16"),
                    convert_encoding("Příliš žluťoučký kůň úpěl ďábelské ódy", "UTF16"),
                    convert_encoding("ipsum dolor", "UTF16")
                ),
                "expected": (
                    convert_encoding("text                                    ", "UTF16"),
                    convert_encoding("lorem                                   ", "UTF16"),
                    convert_encoding("Příliš žluťoučký kůň úpěl ďábelské ódy  ", "UTF16"),
                    convert_encoding("ipsum dolor                             ", "UTF16")
                ),
            },
            "oranvc2": {
                "type": "NVARCHAR2(64)",
                "values": (
                    convert_encoding("text", "UTF16"),
                    convert_encoding("lorem", "UTF16"),
                    convert_encoding("Příliš žluťoučký kůň úpěl ďábelské ódy", "UTF16"),
                    convert_encoding("ipsum dolor", "UTF16")
                ),
            },
            "oranumbr": {
                "type": "NUMBER(38,10)",
                "values": (-12300000.4567891234n, 0.0n, 999888777666.5554443332n, -999888777666.5554443332n),
            },
            "orabinflt": {
                "type": "BINARY_FLOAT",
                "values": (0.00390625, 0.0078125, -0.0078125, 0.0),
            },
            "orabindbl": {
                "type": "BINARY_DOUBLE",
                "values": (1.72, 3.1456789765432, 0.0, -2.7218281828),
            },
            "oratstamp": {
                "type": "TIMESTAMP",
                "values": (2016-01-11T00:00:00, 2016-01-11T05:23:17, 2016-01-11T03:00:20, 2016-01-11T14:15:16),
                "expected": (),
            },
        };

        # MSSQL-specific types. https://msdn.microsoft.com/en-us/library/ms187752.aspx
        hash MssqlColumns = {
            "msvc": {
                "type": "VARCHAR(64)",
                "values": ("text", "lorem", "Příliš žluťoučký kůň úpěl ďábelské ódy", "ipsum dolor"),
                "expected": (
                    "text                                                            ",
                    "lorem                                                           ",
                    "Příliš žluťoučký kůň úpěl ďábelské ódy           ",
                    "ipsum dolor                                                     "
                ),
            },
            "msc": {
                "type": "CHAR(64)",
                "values": ("text", "lorem", "Příliš žluťoučký kůň úpěl ďábelské ódy", "ipsum dolor"),
                "expected": (
                    "text                                                            ",
                    "lorem                                                           ",
                    "Příliš žluťoučký kůň úpěl ďábelské ódy           ",
                    "ipsum dolor                                                     "
                ),
            },
            "msbin": {
                "type": "BINARY(64)",
                "values": (<bead>, <feed>, <beadaabbccdd>, <1122aabb3344ccdd55667788>),
                "expected": (),
            },
            "msvbin": {
                "type": "VARBINARY(64)",
                "values": (<bead>, <feed>, <beadaabbccdd>, <1122aabb3344ccdd55667788>),
            },
            "msblob": {
                "type": "BLOB(64)",
                "values": (<bead>, <feed>, <beadaabbccdd>, <1122aabb3344ccdd55667788>),
            },
            "msbool": {
                "type": "BOOLEAN",
                "values": (True, False, True, True),
                "expected": (1, 0, 1, 1),
            },
            "mstext": {
                "type": "TEXT(128)",
                "values": ("text", "lorem", "Příliš žluťoučký kůň úpěl ďábelské ódy", "ipsum dolor"),
            },
            "msbint": {
                "type": "BIGINT",
                "values": (2200100100, -9223372036854775807, 0, 9223372036854775807),
            },
            "mstint": {
                "type": "TINYINT",
                "values": (1, 0, 255, 128),
            },
            /*"msbit": {
                "type": "BIT",
                "values": (0, 1, 1, 0),
            },*/
            "mstm": {
                "type": "TIME",
                "values": odbc_bind(ODBCT_TIME, (00:00:00Z, 05:23:17Z, 03:59:20Z, 14:15:16Z)),
                "expected": (0s, 5*60m+23m+17s, 3*60m+59m+20s, 14*60m+15m+16s),
            },
            "msdtime": {
                "type": "DATETIME",
                "values": (2016-01-11T00:00:00, 2016-01-11T05:23:17, 2016-01-11T03:00:20, 2016-01-11T14:15:16),
                "expected": (),
            },
        };
    }

    private {
        *Datasource ds;
        const tableName = "odbc_test_table";
        bool mysqlDB = False;
        bool pgsqlDB = False;
        bool oracleDB = False;
        bool mssqlDB = False;
        bool sybaseDB = False;
    }

    constructor() : QUnit::Test("OdbcTest", "1.0", \ARGV) {
        addTestCase("Basic test", \basicTest());
        addTestCase("Types test", \typesTest());
        addTestCase("Timezone test", \timezoneTest());
        addTestCase("Array bind test", \arrayBindTest());
        set_return_value(main());
    }

    destructor() {}

    globalSetUp() {
%ifdef NoOdbc
        printf("ODBC module is not available. Exiting.\n");
        exit(0);
%endif

        try {
            initDatasource();
        }
        catch (hash ex) {
            printf("Exception happened during datasource initialization:\n%s: %s\n", ex.err, ex.desc);
            exit(1);
        }

        prepareDBSpecificInit();
    }

    globalTearDown() {
        if (ds) {
            try {
                ds.exec("DROP TABLE " + tableName);
            }
            catch (hash ex) {
                if (ex.err != "DBI:ODBC:EXEC-ERROR")
                    rethrow;
            }
            ds.commit();
            ds.close();
        }
    }

    string getVersionString(int version) {
        int major = version / 1000000;
        version -= major * 1000000;
        int minor = version / 10000;
        version -= minor * 10000;
        return sprintf("%d.%d.%d", major, minor, version);
    }

    initDatasource() {
        connStr = ENV.QORE_DB_CONNSTR_ODBC;
        if (!connStr) {
            printf("No connection string set. Cannot run the tests.\n");
            exit(1);
        }

        ds = new Datasource(connStr);
        if (ds.getDriverName() != "odbc") {
            printf("Cannot execute the odbc tests on a connection using driver %y.\n", ds.getDriverName());
            exit(1);
        }
        ds.open();

        int serverVer = ds.getServerVersion();
        int clientVer = ds.getClientVersion();
        printf("server version: %s (%d)\n", getVersionString(serverVer), serverVer);
        printf("client version: %s (%d)\n", getVersionString(clientVer), clientVer);
    }

    prepareDBSpecificInit() {
        string typesTable = "odbc_types_test";
        try { ds.exec("DROP TABLE " + typesTable); } catch (e) {}

        # Test if connected to a MySQL database.
        try { # LINESTRING is a MySQL-only datatype.
            ds.exec("CREATE TABLE " + typesTable + " (ls LINESTRING)");
            mysqlDB = True;
            try { ds.exec("DROP TABLE " + typesTable); } catch (e) {}
        }
        catch (hash ex) {
            mysqlDB = False;
        }

        # Test if connected to a PostgreSQL database.
        try { # BYTEA is a PostgreSQL-only datatype.
            ds.exec("CREATE TABLE " + typesTable + " (bt bytea)");
            pgsqlDB = True;
            try { ds.exec("DROP TABLE " + typesTable); } catch (e) {}
        }
        catch (hash ex) {
            pgsqlDB = False;
        }

        # Test if connected to an Oracle database.
        try { # BINARY_FLOAT is an Oracle-only datatype.
            ds.exec("CREATE TABLE " + typesTable + " (bf BINARY_FLOAT)");
            oracleDB = True;
            try { ds.exec("DROP TABLE " + typesTable); } catch (e) {}
        }
        catch (hash ex) {
            oracleDB = False;
        }

        # Test if connected to a MSSQL database.
        try { # IMAGE is a MSSQL-only datatype.
            ds.exec("CREATE TABLE " + typesTable + " (img IMAGE)");
            mssqlDB = True;
            try { ds.exec("DROP TABLE " + typesTable); } catch (e) {}
        }
        catch (hash ex) {
            mssqlDB = False;
        }

        # Test if connected to a Sybase database.
        try { # UNIQUEIDENTIFIERSTR is a Sybase-only datatype.
            ds.exec("CREATE TABLE " + typesTable + " (uis UNIQUEIDENTIFIERSTR)");
            sybaseDB = True;
            try { ds.exec("DROP TABLE " + typesTable); } catch (e) {}
        }
        catch (hash ex) {
            sybaseDB = False;
        }

        if (oracleDB)
            mysqlDB = False;
        if (sybaseDB)
            mssqlDB = False;

        printf("MySQL DB: %s\n", mysqlDB ? "True" : "False");
        printf("PostgreSQL DB: %s\n", pgsqlDB ? "True" : "False");
        printf("Oracle DB: %s\n", oracleDB ? "True" : "False");
        printf("MSSQL DB: %s\n", mssqlDB ? "True" : "False");
        printf("Sybase DB: %s\n", sybaseDB ? "True" : "False");

        # Try additional MySQL types.
        if (mysqlDB) {
            string mysqlTable = "mysql_types_test";
            try { ds.exec("DROP TABLE " + mysqlTable); } catch (e) {}
            HashIterator mysql(MysqlColumns);
            while (mysql.next()) {
                try {
                    ds.exec("CREATE TABLE " + mysqlTable + " (" + mysql.getKey() + " " + mysql.getValue().type + ")");
                }
                catch (hash ex) { continue; }
                try { ds.exec("DROP TABLE " + mysqlTable); } catch (e) {}

                # Add the type to the typesTest Columns.
                Columns{mysql.getKey()} = mysql.getValue();

                if (mysql.getKey() == "mybin") {
                    for (int i = 0; i < Columns.mybin.values.size(); i++) {
                        push Columns.mybin.expected, Columns.mybin.values[i];
                        reference exp = \Columns.mybin.expected[i];
                        for (int j = exp.size(); j < 64; j++)
                            exp += <00>;
                    }
                }
                else if (mysql.getKey() == "mydtime") {
                    for (int i = 0; i < Columns.mydtime.values.size(); i++) {
                        push Columns.mydtime.expected, utcTz.date(Columns.mydtime.values[i]);
                    }
                }
                else if (mysql.getKey() == "mytstamp") {
                    for (int i = 0; i < Columns.mytstamp.values.size(); i++) {
                        push Columns.mytstamp.expected, utcTz.date(Columns.mytstamp.values[i]);
                    }
                }
            }
        }

        # Try additional PostgreSQL types.
        if (pgsqlDB) {
            string pgsqlTable = "pgsql_types_test";
            try { ds.exec("DROP TABLE " + pgsqlTable); } catch (e) {}
            HashIterator pgsql(PgsqlColumns);
            while (pgsql.next()) {
                try {
                    ds.exec("CREATE TABLE " + pgsqlTable + " (" + pgsql.getKey() + " " + pgsql.getValue().type + ")");
                }
                catch (hash ex) { continue; }
                try { ds.exec("DROP TABLE " + pgsqlTable); } catch (e) {}

                # Add the type to the typesTest Columns.
                Columns{pgsql.getKey()} = pgsql.getValue();

                if (pgsql.getKey() == "pgtstamp") {
                    for (int i = 0; i < Columns.pgtstamp.values.size(); i++) {
                        push Columns.pgtstamp.expected, utcTz.date(Columns.pgtstamp.values[i]);
                    }
                }
            }
        }

        # Try additional Oracle types.
        if (oracleDB) {
            string oracleTable = "oracle_types_test";
            try { ds.exec("DROP TABLE " + oracleTable); } catch (e) {}
            HashIterator oracle(OracleColumns);
            while (oracle.next()) {
                try {
                    ds.exec("CREATE TABLE " + oracleTable + " (" + oracle.getKey() + " " + oracle.getValue().type + ")");
                }
                catch (hash ex) { continue; }
                try { ds.exec("DROP TABLE " + oracleTable); } catch (e) {}

                # Add the type to the typesTest Columns.
                Columns{oracle.getKey()} = oracle.getValue();

                if (oracle.getKey() == "oratstamp") {
                    for (int i = 0; i < Columns.oratstamp.values.size(); i++) {
                        push Columns.oratstamp.expected, utcTz.date(Columns.oratstamp.values[i]);
                    }
                }
            }
            Columns.sint.expected = map number($1), Columns.sint.values;
            Columns.i.expected = map number($1), Columns.i.values;
        }

        # Try additional MSSQL types.
        if (mssqlDB) {
            string mssqlTable = "mssql_types_test";
            try { ds.exec("DROP TABLE " + mssqlTable); } catch (e) {}
            HashIterator mssql(MssqlColumns);
            while (mssql.next()) {
                try {
                    ds.exec("CREATE TABLE " + mssqlTable + " (" + mssql.getKey() + " " + mssql.getValue().type + ")");
                }
                catch (hash ex) { continue; }
                try { ds.exec("DROP TABLE " + mssqlTable); } catch (e) {}

                # Add the type to the typesTest Columns.
                Columns{mssql.getKey()} = mssql.getValue();

                if (mssql.getKey() == "msdtime") {
                    for (int i = 0; i < Columns.msdtime.values.size(); i++) {
                        push Columns.msdtime.expected, utcTz.date(Columns.msdtime.values[i]);
                    }
                }
            }
        }

        # Prepare final list of columns and their types.
        foreach hash col in (Columns.pairIterator()) {
            ColumnNamesAndTypes += col.key + " " + col.value.type;
        }
    }

    hash hashLowercaseKeys(hash h) {
        hash nh;
        foreach hash item in (h.pairIterator()) {
            nh{item.key.lwr()} = item.value;
        }
        return nh;
    }

    list hashListLowercaseKeys(list lst) {
        for (int i = 0; i < lst.size(); i++) {
            lst[i] = hashLowercaseKeys(lst[i]);
        }
        return lst;
    }

    basicTest() {
        {
            Datasource test = new Datasource("odbc", "abc", "xyz");
            assertThrows("DBI:ODBC:CONNECTION-ERROR", sub() { test.open(); });
        }

        try {
            ds.exec("DROP TABLE " + tableName);
        }
        catch (hash ex) {
            if (ex.err != "DBI:ODBC:EXEC-ERROR")
                rethrow;
        }
        ds.commit();

        any ret = ds.exec("CREATE TABLE " + tableName + " (id int PRIMARY KEY, txt varchar(128))");
        assertTrue(ret == 0 || ret == -1 || ret.typeCode() == NOTHING);
        ds.commit();

        assertEq(1, ds.exec("INSERT INTO " + tableName + " (id, txt) VALUES (1, 'abcdef123456')"));
        ds.commit();

        assertEq(1, ds.exec("INSERT INTO " + tableName + " (id, txt) VALUES (%v, %v)", 2, "test 789 abc lorem ipsum"));
        ds.commit();

        assertThrows("DBI:ODBC:BIND-ERROR", "not enough parameters passed for binding",
            sub() { ds.exec("INSERT INTO " + tableName + " (id, txt) VALUES (%v, %v)", 3); });
        assertThrows("DBI:ODBC:BIND-ERROR", "hash parameter not in correct format for odbc_bind",
            sub() { ds.exec("INSERT INTO " + tableName + " (id, txt) VALUES (%v, %v)", 3, {}); });

        assertThrows("DBI:ODBC:EXEC-ERROR", "error during statement execution", sub() { ds.exec("bla bla bla"); });

        hash results1 = {"id": (1, 2), "txt": ("abcdef123456", "test 789 abc lorem ipsum")};
        list results2 = ({"id": 1, "txt": "abcdef123456"}, {"id": 2, "txt": "test 789 abc lorem ipsum"});

        ret = ds.exec("SELECT * FROM " + tableName);
        if (oracleDB)
            ret = hashLowercaseKeys(ret);
        assertEq(results1, ret);

        ret = ds.select("SELECT * FROM " + tableName);
        if (oracleDB)
            ret = hashLowercaseKeys(ret);
        assertEq(results1, ret);

        ret = ds.selectRows("SELECT * FROM " + tableName);
        if (oracleDB)
            ret = hashListLowercaseKeys(ret);
        assertEq(results2, ret);

        ds.exec("DROP TABLE " + tableName);
        ds.commit();
    }

    typesTest() {
        printf("\n**************************\n");
        printf("The following columns and their types are tested in the 'types test':\n");
        foreach string col in (ColumnNamesAndTypes.iterator()) {
            printf("%s\n", col);
        }
        printf("**************************\n\n");

        ds.setOption("numeric-numbers", True);
        try {
            ds.exec("DROP TABLE " + tableName);
        }
        catch (hash ex) {
            if (ex.err != "DBI:ODBC:EXEC-ERROR")
                rethrow;
        }
        ds.commit();

        string def = sprintf(" (%s)", foldl $1 + ", " + $2, ColumnNamesAndTypes);
        any ret = ds.exec("CREATE TABLE " + tableName + def);
        assertTrue(ret == 0 || ret == -1 || ret.typeCode() == NOTHING);
        ds.commit();

        list arguments = ();
        foreach hash col in (Columns.pairIterator()) {
            push arguments, col.value.values;
        }

        string valdef = "";
        for (int i = 0; i < ColumnNamesAndTypes.size(); i++)
            valdef += "%v,";
        valdef = valdef.substr(0, valdef.size()-1);

        SQLStatement stmt = new SQLStatement(ds);
        stmt.prepare("INSERT INTO " + tableName + sprintf(" (%s) VALUES (", foldl $1 + ", " + $2, Columns.keys()) + valdef + ")");
        stmt.bindArgs(arguments);
        stmt.exec();
        stmt.commit();

        hash results = Columns;
        foreach string key in (results.keys()) {
            if (exists results{key}.expected)
                results{key} = results{key}.expected;
            else
                results{key} = results{key}.values;
        }

        ret = ds.exec("SELECT * FROM " + tableName);
        if (oracleDB)
            ret = hashLowercaseKeys(ret);
        assertEq(results, ret);

        ds.exec("DROP TABLE " + tableName);
        ds.commit();
    }

    timezoneTest() {
        Datasource tzDs = new Datasource(connStr);
        tzDs.setOption("qore-timezone", "Asia/Tokyo");
        tzDs.open();

        try {
            tzDs.exec("DROP TABLE " + tableName);
        }
        catch (hash ex) {
            if (ex.err != "DBI:ODBC:EXEC-ERROR")
                rethrow;
        }
        tzDs.commit();

        any ret = tzDs.exec("CREATE TABLE " + tableName + " (id int, t " + (mssqlDB ? "datetime)" : "timestamp)"));
        assertTrue(ret == 0 || ret == -1 || ret.typeCode() == NOTHING);
        tzDs.commit();

        assertEq(1, tzDs.exec("INSERT INTO " + tableName + " (id, t) VALUES (%v, %v)", 1, 2016-01-11T00:00:00Z));
        tzDs.commit();
        assertEq(1, tzDs.exec("INSERT INTO " + tableName + " (id, t) VALUES (%v, %v)", 2, 2016-01-11T09:00:00+09:00));
        tzDs.commit();

        hash h = {"id": (1, 2), "t": (2016-01-11T09:00:00+09:00, 2016-01-11T09:00:00+09:00)};

        ret = tzDs.select("SELECT * FROM " + tableName);
        if (oracleDB)
            ret = hashLowercaseKeys(ret);
        assertEq(h, ret);

        tzDs.close();
    }

    arrayBindTest() {
        try {
            ds.exec("DROP TABLE " + tableName);
        }
        catch (hash ex) {
            if (ex.err != "DBI:ODBC:EXEC-ERROR")
                rethrow;
        }
        ds.commit();

        any ret = ds.exec("CREATE TABLE " + tableName + " (id int, txt varchar(48))");
        assertTrue(ret == 0 || ret == -1 || ret.typeCode() == NOTHING);
        ds.commit();

        SQLStatement stmt = new SQLStatement(ds);
        stmt.prepare("INSERT INTO " + tableName + " (id, txt) VALUES (%v, %v)");

        stmt.bindArgs((2, ("text", "abc", "lorem")));
        stmt.exec();
        stmt.commit();

        stmt.bindArgs(((4, 5), ("ipsum", "dolor")));
        stmt.exec();
        stmt.commit();

        stmt.execArgs( ((6,), "sit") );
        stmt.commit();

        hash h = {"id": (2, 2, 2, 4, 5, 6), "txt": ("text", "abc", "lorem", "ipsum", "dolor", "sit")};
        if (oracleDB)
            h.id = (2n, 2n, 2n, 4n, 5n, 6n);

        ret = ds.select("SELECT * FROM " + tableName);
        if (oracleDB)
            ret = hashLowercaseKeys(ret);
        assertEq(h, ret);
    }
}
